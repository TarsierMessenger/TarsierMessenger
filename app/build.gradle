apply plugin: "com.android.application"
apply plugin: "checkstyle"
apply plugin: "jacoco"

android {
    compileSdkVersion 18
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildToolsVersion "21.1.1"
    defaultConfig {
        applicationId "ch.tarsier.tarsier"
        minSdkVersion 17
        targetSdkVersion 17
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
        testHandleProfiling true
        testFunctionalTest true
    }
    
    sourceSets {
      main {
        jniLibs.srcDirs = ['libs']
        jni.srcDirs = [] 
      }
    }    

    buildTypes {
        debug {
            minifyEnabled false
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    productFlavors {
    }

    lintOptions {
        textReport true
        textOutput 'stdout'

        // Needed because Espresso references APIs that are not supported in Android.
        ignore 'InvalidPackage',
               'GradleDependency',
               'OldTargetApi',
               'LabelFor',
               'IconDipSize',
               'IconDuplicatesConfig',
               'UselessParent',
               'IconColors',
               'TrulyRandom',
               'SimpleDateFormat',
               'IconMissingDensityFolder',
               'Overdraw',
               'MergeRootFrame'
    }
}

check.finalizedBy('jacocoTestReport')

task jacocoTestReport(type: JacocoReport, dependsOn: "createDebugCoverageReport") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."

    reports {
        xml.enabled = true
        html.enabled = true
    }
    classDirectories = fileTree(
            dir: './build/intermediates/classes/debug',
            excludes: ['**/R*.class',
                       '**/*$InjectAdapter.class',
                       '**/*$ModuleAdapter.class',
                       '**/*$ViewInjector*.class',
                       '**/FillDatabaseWithFictionalData.class',
                       '**/google/protobuf/**/*.class',
                       '**/TarsierWireProtos.class',
                       '**/BuildConfig.class',
                       '**/network/messages/**/*.class',
                       '**/*Test*.class'
            ])
    sourceDirectories = files(['src/main/java/'])
    executionData = files("./build/outputs/code-coverage/connected/coverage.ec")
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile files('libs/espresso-contrib-1.1-bundled.jar')
    compile files('../libs/espresso-contrib-1.1-bundled.jar')
    androidTestCompile files('libs/com.springsource.org.objenesis-1.0.0.jar')
    compile files('../libs/com.springsource.org.objenesis-1.0.0.jar')
    androidTestCompile files('libs/mockito-core-1.9.5.jar')
    compile files('../libs/mockito-core-1.9.5.jar')
    androidTestCompile files('libs/dexmaker-1.0.jar')
    compile files('../libs/dexmaker-1.0.jar')
    androidTestCompile files('libs/dexmaker-mockito-1.0.jar')
    compile files('../libs/dexmaker-mockito-1.0.jar')
    androidTestCompile files('libs/otto-1.3.5.jar')
    compile files('../libs/otto-1.3.5.jar')
}

check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle) {
    ignoreFailures = false
    configFile file("config/checkstyle/checkstyle.xml")

    source 'src'
    include '**/*.java'
    exclude '**/FillDatabaseWithFictionalData.java',
            '**/com/google/**',
            '**/TarsierWireProtos.java',
            'androidTest/**'
            
    classpath = files()
}
